<application> ::= AppBuilder(<app_name>)
                  <brick_decl>*
                  <state_decl>+
                  .get_contents()

<brick_decl> ::= <sensor_decl> | <actuator_decl>

<sensor_decl> ::= .sensor(<name>).on_pin(<pin_number>)

<actuator_decl> ::= .actuator(<name>).on_pin(<pin_number>)

<state_decl> ::= .state(<state_name>)
                 <action>+
                 <transition>

<action> ::= .set(<actuator_name>).to(<signal>)

<transition> ::= <simple_transition>
               | <and_transition>
               | <or_transition>

<simple_transition> ::= .when(<sensor_name>)
                        .has_value(<signal>)
                        .go_to_state(<state_name>)

<and_transition> ::= .when_all(<condition_list>)
                     .go_to_state(<state_name>)

<or_transition> ::= .when_any(<condition_list>)
                    .go_to_state(<state_name>)

<condition_list> ::= <condition_tuple> (, <condition_tuple>)*

<condition_tuple> ::= (<sensor_name>, <signal>)

<app_name>       ::= <string>
<name>           ::= <string>
<state_name>     ::= <string>
<sensor_name>    ::= <string>
<actuator_name>  ::= <string>
<pin_number>     ::= <number>
<signal>         ::= HIGH | LOW
<string>         ::= "[^"]*"
<number>         ::= [0-9]+
